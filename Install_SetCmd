; $VER: Installation script for SetCmd (1.0.0)
; vim: set syntax=lisp ts=2 expandtab:

(set @app-name "SetCMD (1.0.0)")

; I have only currently tested this on a few OS 4 systems.
; There will be an update at some point that will support classic AmigaOS, but until then this
; check needs to stay. For versions, I used https://wiki.amigaos.net/wiki/AmigaOS_Versions .
(if (< (/ (getversion) 65536) 52)
  (
    (abort "SetCmd has only been tested on OS 4.x (v52+), so not installing on this OS!")
   )
)

(message 
  "Welcome to the SetCmd Installer!\n"
  "This will install the SetCmd utility and documentation on your system.\n\n"
  "NOTE: It will not automatically initialise SetCmd for you, for information on how to do this\n"
  "please make sure you read the included documentation.\n"
)

(welcome)
(procedure get_and_create_destination
  (
    (set #dname
      (askdir
        (prompt (cat "Choose destination directory.\n"
          "A drawer called SetCmd will be created." ))
        (help @askdir-help)
        (default "Work:")
      )
    )
    (set #dname (tackon #dname "SetCmd"))
    ; This is the built-in variable that gets displayed to the user at the end of the installation
    (set @default-dest #dname)

    (makedir #dname
      (prompt ("Create drawer %s?" #dname))
      (help @makedir-help)
      (safe)
    )
    (makedir (tackon #dname "support"))
    
    (makeassign "SETCMD" #dname (safe))
    (set #new_install 1)
  )
)


(get_and_create_destination)
(set #dname (getassign "SETCMD" "a"))


(copyfiles
  (source "setcmd")
  (dest #dname)
  (prompt ("Copy SetCmd program file?"))
  (confirm "expert")
  (all)
  (help @copyfiles-help)
)

(copyfiles
  (source "stub")
  (dest #dname)
  (prompt ("Copy SetCmd stub file?"))
  (confirm "expert")
  (all)
  (help @copyfiles-help)
)

(copyfiles
  (source "")
  (pattern "README.txt#?")
  (dest #dname)
  (prompt ("Copy SetCmd README.txt documentation?"))
  (confirm "expert")
  (help @copyfiles-help)
)

(copyfiles
  (source "")
  (pattern "SetCmd.guide#?")
  (dest #dname)
  (prompt ("Copy SetCmd AmigaGuide documentation?"))
  (confirm "expert")
  (help @copyfiles-help)
)

; Set up the link database directory structure
(makedir (tackon #dname "path"))
(makedir (tackon #dname "cmds"))
(makedir (tackon (tackon #dname "cmds") "setcmd"))
(run (cat "C:MakeLink FROM \"" (tackon #dname "cmds/setcmd/release") "\" TO SETCMD:setcmd SOFT"))
(run (cat "C:MakeLink FROM \"" (tackon #dname "path/setcmd") "\" TO SETCMD:cmds/setcmd/release SOFT"))
